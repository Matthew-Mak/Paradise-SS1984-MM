/////////////////////
// DISABILITY GENES
//
// These activate either a mutation, disability
//
// Gene is always activated.
/////////////////////

/datum/dna/gene/disability
	name = "DISABILITY"

	/// Activation message
	var/list/activation_message

	/// Yay, you're no longer growing 3 arms
	var/list/deactivation_message


/datum/dna/gene/disability/can_activate(mob/living/mutant, flags)
	return TRUE // Always set!


/datum/dna/gene/disability/activate(mob/living/mutant, flags)
	. = ..()
	if(length(activation_message))
		var/msg = pick(activation_message)
		to_chat(mutant, span_warning("[msg]"))
	else
		testing("[name] has no activation message.")


/datum/dna/gene/disability/deactivate(mob/living/mutant, flags)
	. = ..()
	if(length(deactivation_message))
		var/msg = pick(deactivation_message)
		to_chat(mutant, span_warning("[msg]"))
	else
		testing("[name] has no deactivation message.")


/datum/dna/gene/disability/hallucinate
	name = "Hallucinate"
	activation_message = list("–í–∞—à —Ä–∞–∑—É–º –≥–æ–≤–æ—Ä–∏—Ç: ¬´–ü—Ä–∏–≤–µ—Ç!¬ª.")
	deactivation_message = list("–ó–¥—Ä–∞–≤–æ–º—ã—Å–ª–∏–µ –≤–æ–∑–≤—Ä–∞—â–∞–µ—Ç—Å—è. –ò–ª–∏ –Ω–µ—Ç?")
	instability = -GENE_INSTABILITY_MODERATE


/datum/dna/gene/disability/hallucinate/New()
	..()
	block = GLOB.hallucinationblock


/datum/dna/gene/disability/hallucinate/OnMobLife(mob/living/carbon/human/H)
	if(prob(1))
		H.AdjustHallucinate(45 SECONDS)
		H.last_hallucinator_log = "Hallucination Gene"


/datum/dna/gene/disability/epilepsy
	name = "Epilepsy"
	activation_message = list("–£ –≤–∞—Å —Ä–∞–∑–±–æ–ª–µ–ª–∞—Å—å –≥–æ–ª–æ–≤–∞.")
	deactivation_message = list("–í–∞—à–∞ –≥–æ–ª–æ–≤–∞ –ø–µ—Ä–µ—Å—Ç–∞–ª–∞ –±–æ–ª–µ—Ç—å. –ù–∞–∫–æ–Ω–µ—Ü-—Ç–æ!")
	instability = -GENE_INSTABILITY_MODERATE


/datum/dna/gene/disability/epilepsy/New()
	..()
	block = GLOB.epilepsyblock


/datum/dna/gene/disability/epilepsy/OnMobLife(mob/living/carbon/human/H)
	if((prob(1) && H.AmountParalyzed() < 2 SECONDS))
		H.visible_message(span_danger("[H] –Ω–∞—á–∏–Ω–∞[pluralize_ru(H.gender, "–µ—Ç", "—é—Ç")] –±–∏—Ç—å—Å—è –≤ –ø—Ä–∏–ø–∞–¥–∫–µ!"), span_alert("–£ –≤–∞—Å –ø—Ä–∏–ø–∞–¥–æ–∫!"))
		H.Paralyse(20 SECONDS)
		H.Jitter(2000 SECONDS)


/datum/dna/gene/disability/cough
	name = "Coughing"
	activation_message = list("–í—ã –Ω–∞—á–∏–Ω–∞–µ—Ç–µ –∫–∞—à–ª—è—Ç—å.")
	deactivation_message = list("–í–∞—à–µ –≥–æ—Ä–ª–æ –ø–µ—Ä–µ—Å—Ç–∞–ª–æ –±–æ–ª–µ—Ç—å.")
	instability = -GENE_INSTABILITY_MINOR


/datum/dna/gene/disability/cough/New()
	..()
	block = GLOB.coughblock


/datum/dna/gene/disability/cough/OnMobLife(mob/living/carbon/human/H)
	if((prob(5) && H.AmountParalyzed() <= 2 SECONDS))
		H.drop_from_active_hand()
		H.emote("cough")


/datum/dna/gene/disability/clumsy
	name = "Clumsiness"
	activation_message = list("–í—ã —á—É–≤—Å—Ç–≤—É–µ—Ç–µ –ª—ë–≥–∫–æ–µ –≥–æ–ª–æ–≤–æ–∫—Ä—É–∂–µ–Ω–∏–µ.")
	deactivation_message = list("–í—ã –≤–Ω–æ–≤—å –æ–±—Ä–µ—Ç–∞–µ—Ç–µ –∫–æ–Ω—Ç—Ä–æ–ª—å –Ω–∞–¥ —Å–≤–æ–∏–º–∏ –¥–≤–∏–∂–µ–Ω–∏—è–º–∏.")
	instability = -GENE_INSTABILITY_MINOR
	traits_to_add = list(TRAIT_CLUMSY)


/datum/dna/gene/disability/clumsy/New()
	..()
	block = GLOB.clumsyblock


/datum/dna/gene/disability/tourettes
	name = "Tourettes"
	activation_message = list("–ù–∞—Ö–ª—ã–Ω—É–ª–∞ –∫–∞–∫–∞—è-—Ç–æ –Ω–µ–ø–æ–Ω—è—Ç–Ω–∞—è –¥—Ä–æ–∂—å...")
	deactivation_message = list("–í—ã —á—É–≤—Å—Ç–≤—É–µ—Ç–µ –≤–∫—É—Å –º—ã–ª–∞ –≤–æ —Ä—Ç—É.")
	instability = -GENE_INSTABILITY_MODERATE


/datum/dna/gene/disability/tourettes/New()
	..()
	block = GLOB.twitchblock


/datum/dna/gene/disability/tourettes/OnMobLife(mob/living/carbon/human/H)
	if((prob(10) && H.AmountParalyzed() <= 2 SECONDS))
		H.Stun(20 SECONDS)
		switch(rand(1, 3))
			if(1)
				H.emote("twitch")
			if(2 to 3)
				H.say("[prob(50) ? ";" : ""][pick("–ì–û–í–ù–û", "–ú–û–ß–ê", "–ë–õ–Ø–¢–¨", "–ü–ò–ó–î–ê", "–•–£–ï–°–û–°", "–í–´–ë–õ–Ø–î–û–ö", "–•–£–ô", "–•–û–° –•–£–ï–°–û–°", "–°–£–ö–ê", "–ü–û–®–Å–õ –ù–ê–•–£–ô", "–•–ï–†–ù–Ø", "–ö–û–ö–ü–ò–¢–ê–ù", "–î–û–õ–ë–ê–Å–ë", "–ï–ë–õ–Ø", "–ù–ê–ú –ü–ò–ó–î–ê")]")
		var/x_offset_old = H.pixel_x
		var/y_offset_old = H.pixel_y
		var/x_offset = H.pixel_x + rand(-2, 2)
		var/y_offset = H.pixel_y + rand(-1, 1)
		animate(H, pixel_x = x_offset, pixel_y = y_offset, time = 1)
		animate(H, pixel_x = x_offset_old, pixel_y = y_offset_old, time = 1)


/datum/dna/gene/disability/nervousness
	name = "Nervousness"
	activation_message = list("–í—ã –Ω–∞—á–∏–Ω–∞–µ—Ç–µ –Ω–µ—Ä–≤–Ω–∏—á–∞—Ç—å.")
	deactivation_message = list("–í—ã —á—É–≤—Å—Ç–≤—É–µ—Ç–µ —Å–µ–±—è –≥–æ—Ä–∞–∑–¥–æ —Å–ø–æ–∫–æ–π–Ω–µ–µ.")


/datum/dna/gene/disability/nervousness/New()
	..()
	block = GLOB.nervousblock


/datum/dna/gene/disability/nervousness/OnMobLife(mob/living/carbon/human/H)
	if(prob(10))
		H.Stuttering(20 SECONDS)


/datum/dna/gene/disability/blindness
	name = "Blindness"
	activation_message = list("–í–∏–¥–∏–º–æ, –≤—ã –±–æ–ª—å—à–µ –Ω–∏—á–µ–≥–æ –Ω–µ –≤–∏–¥–∏—Ç–µ.")
	deactivation_message = list("–¢–µ–ø–µ—Ä—å –≤—ã –º–æ–∂–µ—Ç–µ –≤–∏–¥–µ—Ç—å, –µ—Å–ª–∏ –≤–¥—Ä—É–≥ –Ω–µ –∑–∞–º–µ—Ç–∏–ª–∏...")
	instability = -GENE_INSTABILITY_MAJOR
	traits_to_add = list(TRAIT_BLIND)


/datum/dna/gene/disability/blindness/New()
	..()
	block = GLOB.blindblock


/datum/dna/gene/disability/blindness/activate(mob/living/mutant, flags)
	. = ..()
	mutant.update_blind_effects()


/datum/dna/gene/disability/blindness/deactivate(mob/living/mutant, flags)
	. = ..()
	mutant.update_blind_effects()


/datum/dna/gene/disability/colourblindness
	name = "Colourblindness"
	activation_message = list("–í—ã —á—É–≤—Å—Ç–≤—É–µ—Ç–µ —Å—Ç—Ä–∞–Ω–Ω–æ–µ –ø–æ–∫–∞–ª—ã–≤–∞–Ω–∏–µ –≤ –≥–ª–∞–∑–∞—Ö. –í–∞—à–µ –≤–æ—Å–ø—Ä–∏—è—Ç–∏–µ —Ü–≤–µ—Ç–∞ –º–µ–Ω—è–µ—Ç—Å—è.")
	deactivation_message = list("–í—ã —á—É–≤—Å—Ç–≤—É–µ—Ç–µ –Ω–µ–ø—Ä–∏—è—Ç–Ω–æ–µ –ø–æ–∫–∞–ª—ã–≤–∞–Ω–∏–µ –≤ –≥–ª–∞–∑–∞—Ö, –Ω–æ –≤—Å–µ –≤–æ–∫—Ä—É–≥ –≤–Ω–æ–≤—å –æ–±—Ä–µ–ª–æ –∫—Ä–∞—Å–∫–∏.")
	instability = -GENE_INSTABILITY_MODERATE
	traits_to_add = list(TRAIT_COLORBLIND)


/datum/dna/gene/disability/colourblindness/New()
	..()
	block = GLOB.colourblindblock


/datum/dna/gene/disability/colourblindness/activate(mob/living/mutant, flags)
	. = ..()
	mutant.update_client_colour()	//Handle the activation of the colourblindness on the mob.
	mutant.update_misc_effects()	//Apply eyeshine as needed.


/datum/dna/gene/disability/colourblindness/deactivate(mob/living/carbon/human/mutant, flags)
	. = ..()
	mutant.update_client_colour()	//Handle the deactivation of the colourblindness on the mob.
	mutant.update_misc_effects()	//Remove eyeshine as needed.


/datum/dna/gene/disability/deaf
	name = "Deafness"
	activation_message = list("–ó–¥–µ—Å—å –∫–∞–∫-—Ç–æ —Ç–∏—Ö–æ...")
	deactivation_message = list("–í—ã —Å–Ω–æ–≤–∞ –º–æ–∂–µ—Ç–µ —Å–ª—ã—à–∞—Ç—å!")
	instability = -GENE_INSTABILITY_MAJOR
	traits_to_add = list(TRAIT_DEAF)


/datum/dna/gene/disability/deaf/New()
	..()
	block = GLOB.deafblock


/datum/dna/gene/disability/nearsighted
	name = "Nearsightedness"
	activation_message = list("–í—Å—ë –≤–æ–∫—Ä—É–≥ –Ω–∞—á–∏–Ω–∞–µ—Ç —Ä–∞–∑–º—ã–≤–∞—Ç—å—Å—è...")
	deactivation_message = list("–¢–µ–ø–µ—Ä—å –≤—ã –º–æ–∂–µ—Ç–µ —è—Å–Ω–æ –≤–∏–¥–µ—Ç—å.")
	instability = -GENE_INSTABILITY_MODERATE
	traits_to_add = list(TRAIT_NEARSIGHTED)


/datum/dna/gene/disability/nearsighted/New()
	..()
	block = GLOB.glassesblock


/datum/dna/gene/disability/nearsighted/activate(mob/living/mutant, flags)
	. = ..()
	mutant.update_nearsighted_effects()


/datum/dna/gene/disability/nearsighted/deactivate(mob/living/mutant, flags)
	. = ..()
	mutant.update_nearsighted_effects()


/datum/dna/gene/disability/lisp
	name = "Lisp"
	desc = "–ò–Ω—Ç–µ—Ä–µ—Ñ–Ω–æ, —Ñ—Ç–æ —ç—Ç–æ –¥–µ–ª–∞–µ—Ç."
	activation_message = list("–§—Ç–æ-—Ç–æ —Ç–æ—à–Ω–æ –Ω–µ —Ç–∞—Ö.")
	deactivation_message = list("–¢–µ–ø–µ—Ä—å –≤—ã –º–æ–∂–µ—Ç–µ –ø—Ä–æ–∏–∑–Ω–æ—Å–∏—Ç—å —Å–æ–≥–ª–∞—Å–Ω—ã–µ.")


/datum/dna/gene/disability/lisp/New()
	..()
	block = GLOB.lispblock


/datum/dna/gene/disability/lisp/OnSay(mob/M, message)
	return replacetext(message,"—Å",pick("—â","—à","—Ñ"))


/datum/dna/gene/disability/comic
	name = "Comic"
	desc = "–≠—Ç–æ –ø—Ä–∏–Ω–µ—Å–µ—Ç —Ç–æ–ª—å–∫–æ —Å–º–µ—Ä—Ç—å –∏ —Ä–∞–∑—Ä—É—à–µ–Ω–∏–µ."
	activation_message = list(span_sans("–û–π-–π–æ!"))
	deactivation_message = list("–°–ª–∞–≤–∞ –°–≤—è—Ç–æ–π –•–æ–Ω–∫–æ–º–∞—Ç–µ—Ä–∏, —Å —ç—Ç–∏–º –ø–æ–∫–æ–Ω—á–µ–Ω–æ.")
	traits_to_add = list(TRAIT_COMIC)


/datum/dna/gene/disability/comic/New()
	..()
	block = GLOB.comicblock


/datum/dna/gene/disability/wingdings
	name = "Alien Voice"
	desc = "–ò—Å–∫–∞–∂–∞–µ—Ç –≥–æ–ª–æ—Å —Å—É–±—ä–µ–∫—Ç–∞, –ø—Ä–µ–≤—Ä–∞—â–∞—è –µ–≥–æ –≤ –Ω–µ–ø–æ–Ω—è—Ç–Ω—É—é —Ä–µ—á—å."
	activation_message = list("‚ú°Ô∏é‚ñ°Ô∏é‚óÜÔ∏é‚ùíÔ∏é ‚ùñÔ∏é‚ñ°Ô∏é‚ôçÔ∏é‚ôãÔ∏é‚óèÔ∏é ‚ôçÔ∏é‚ñ°Ô∏é‚ùíÔ∏é‚ôéÔ∏é‚¨ßÔ∏é ‚¨ßÔ∏é‚ôèÔ∏é‚ôèÔ∏é‚ùçÔ∏é ‚ôãÔ∏é‚óèÔ∏é‚ôìÔ∏é‚ôèÔ∏é‚ñ†Ô∏éüì¨Ô∏é") // Your vocal cords seem alien.
	deactivation_message = list("–í–∞—à–∏ –≥–æ–ª–æ—Å–æ–≤—ã–µ —Å–≤—è–∑–∫–∏ –±–æ–ª—å—à–µ –Ω–µ –∫–∞–∂—É—Ç—Å—è –∏–Ω–æ—Ä–æ–¥–Ω—ã–º–∏.")
	instability = -GENE_INSTABILITY_MINOR
	traits_to_add = list(TRAIT_WINGDINGS)


/datum/dna/gene/disability/wingdings/New()
	..()
	block = GLOB.wingdingsblock


/datum/dna/gene/disability/wingdings/OnSay(mob/M, message)
	var/garbled_message = ""
	var/i = 1
	while(i <= length(message))
		var/char = lowertext(message[i])
		if(char in GLOB.alphabet)
			if(prob(50)) // upper and lowercase chars have different symbols, we encrypt the word and mix them up
				garbled_message += pick(GLOB.alphabet_uppercase)
			else
				garbled_message += pick(GLOB.alphabet)
		else if(char in GLOB.alphabet_cyrillic)
			if(prob(50))
				garbled_message += pick(GLOB.alphabet_uppercase)
			else
				garbled_message += pick(GLOB.alphabet)
			i++ // rus chars coded by 2 bytes, so we need to skip one byte when encrypting them
		else
			garbled_message += message[i]
		i++
	message = garbled_message
	return message


/datum/dna/gene/disability/weak
	name = "Weak"
	desc = "–î–µ–ª–∞–µ—Ç –º—ã—à—Ü—ã —Å—É–±—ä–µ–∫—Ç–∞ –±–æ–ª–µ–µ —Å–ª–∞–±—ã–º–∏."
	activation_message = list("–í—ã —á—É–≤—Å—Ç–≤—É–µ—Ç–µ –≤–Ω–µ–∑–∞–ø–Ω—É—é —Å–ª–∞–±–æ—Å—Ç—å –≤ –º—ã—à—Ü–∞—Ö.")
	deactivation_message = list("–í—ã —Å–Ω–æ–≤–∞ –æ—â—É—â–∞–µ—Ç–µ —Å–∏–ª—É –≤ –º—ã—à—Ü–∞—Ö.")
	instability = -GENE_INSTABILITY_MODERATE
	traits_to_add = list(TRAIT_GENE_WEAK)


/datum/dna/gene/disability/weak/New()
	..()
	block = GLOB.weakblock


/datum/dna/gene/disability/weak/can_activate(mob/living/mutant, flags)
	if(!ishuman(mutant) || HAS_TRAIT(mutant, TRAIT_GENE_STRONG))
		return FALSE
	return ..()


/datum/dna/gene/disability/weak/activate(mob/living/carbon/human/mutant, flags)
	. = ..()
	RegisterSignal(mutant, COMSIG_HUMAN_SPECIES_CHANGED, PROC_REF(on_species_change))
	add_weak_modifiers(mutant)


/datum/dna/gene/disability/weak/deactivate(mob/living/carbon/human/mutant, flags)
	. = ..()
	UnregisterSignal(mutant, COMSIG_HUMAN_SPECIES_CHANGED)
	remove_weak_modifiers(mutant)


/datum/dna/gene/disability/weak/proc/on_species_change(mob/living/carbon/human/mutant, datum/species/old_species)
	SIGNAL_HANDLER

	if(old_species.name != mutant.dna.species.name)
		remove_weak_modifiers(mutant, old_species)
		add_weak_modifiers(mutant)


/datum/dna/gene/disability/weak/proc/add_weak_modifiers(mob/living/carbon/human/mutant)
	mutant.physiology.tail_strength_mod *= 0.75
	switch(mutant.dna.species.name)
		if(SPECIES_VULPKANIN, SPECIES_DRASK, SPECIES_UNATHI)
			mutant.physiology.grab_resist_mod *= 0.75
			mutant.physiology.punch_damage_low -= 3
			mutant.physiology.punch_damage_high -= 4
		if(SPECIES_HUMAN)
			mutant.physiology.grab_resist_mod *= 0.9
			mutant.physiology.punch_damage_low -= 1
			mutant.physiology.punch_damage_high -= 2
		else
			mutant.physiology.grab_resist_mod *= 0.85
			mutant.physiology.punch_damage_low -= 2
			mutant.physiology.punch_damage_high -= 3


/datum/dna/gene/disability/weak/proc/remove_weak_modifiers(mob/living/carbon/human/mutant, datum/species/species)
	if(!species)
		species = mutant.dna.species
	mutant.physiology.tail_strength_mod /= 0.75
	switch(species.name)
		if(SPECIES_VULPKANIN, SPECIES_DRASK, SPECIES_UNATHI)
			mutant.physiology.grab_resist_mod /= 0.75
			mutant.physiology.punch_damage_low += 3
			mutant.physiology.punch_damage_high += 4
		if(SPECIES_HUMAN)
			mutant.physiology.grab_resist_mod /= 0.9
			mutant.physiology.punch_damage_low += 1
			mutant.physiology.punch_damage_high += 2
		else
			mutant.physiology.grab_resist_mod /= 0.85
			mutant.physiology.punch_damage_low += 2
			mutant.physiology.punch_damage_high += 3

/datum/dna/gene/disability/paraplegia
	name = "Paraplegia"
	desc = "–ü–∞—Ä–∞–ª–∏–∑—É–µ—Ç –º—ã—à—Ü—ã –Ω–æ–≥."
	activation_message = list("–í—ã –Ω–µ —á—É–≤—Å—Ç–≤—É–µ—Ç–µ —Å–≤–æ–∏—Ö –Ω–æ–≥.")
	deactivation_message = list("–í—ã –≤–æ–∑–≤—Ä–∞—â–∞–µ—Ç–µ –∫–æ–Ω—Ç—Ä–æ–ª—å –Ω–∞–¥ –Ω–æ–≥–∞–º–∏.")
	instability = -GENE_INSTABILITY_MAJOR
	traits_to_add = list(TRAIT_FLOORED)

/datum/dna/gene/disability/paraplegia/New()
	..()
	block = GLOB.paraplegiablock
